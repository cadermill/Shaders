Shader "Custom/DigitalImpressionism"
{
    Properties
    {
        [MainColor] _BaseColor("Base Color", Color) = (1, 1, 1, 1)
        [MainTexture] _BaseMap("Base Map", 2D) = "white"

        _CellSize ("Cell Size", Float) = 0.1
    }

    SubShader
    {
        Tags { "RenderType" = "Opaque" "RenderPipeline" = "UniversalPipeline" }

        Pass
        {
            HLSLPROGRAM

            #pragma vertex vert
            #pragma fragment frag

            #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Core.hlsl"

            struct Attributes
            {
                float4 positionOS : POSITION;
                float2 uv : TEXCOORD0;
            };

            struct Varyings
            {
                float4 positionHCS : SV_POSITION;
                float2 uv : TEXCOORD0;
                float3 positionOS : TEXCOORD1;
            };

            TEXTURE2D(_BaseMap);
            SAMPLER(sampler_BaseMap);

            CBUFFER_START(UnityPerMaterial)
                half4 _BaseColor;
                float4 _BaseMap_ST;

                float _CellSize;
            CBUFFER_END

            float rand3dTo1d(float3 value, float3 dotDir = float3(12.9898, 78.233, 37.719))
            {
	            //make value smaller to avoid artefacts
	            float3 smallValue = sin(value);
	            //get scalar value from 3d vector
	            float random = dot(smallValue, dotDir);
	            //make value more random by making it bigger and then taking the factional part
	            random = frac(sin(random) * 143758.5453);
	            return random;
            }

            float3 rand3dTo3d(float3 value){
	            return float3(
		            rand3dTo1d(value, float3(12.989, 78.233, 37.719)),
		            rand3dTo1d(value, float3(39.346, 11.135, 83.155)),
		            rand3dTo1d(value, float3(73.156, 52.235, 09.151))
	            );
            }

            float rand1dTo1d(float value, float mutator = 0.546){
	            float random = frac(sin(value + mutator) * 143758.5453);
	            return random;
            }

            float3 voronoiNoise(float3 value){
                float3 baseCell = floor(value);

                //first pass to find the closest cell
                float minDistToCell = 10;
                float3 toClosestCell;
                float3 closestCell;
                [unroll]
                for(int x1=-1; x1<=1; x1++){
                    [unroll]
                    for(int y1=-1; y1<=1; y1++){
                        [unroll]
                        for(int z1=-1; z1<=1; z1++){
                            float3 cell = baseCell + float3(x1, y1, z1);
                            float3 cellPosition = cell + rand3dTo3d(cell);
                            float3 toCell = cellPosition - value;
                            float distToCell = length(toCell);
                            if(distToCell < minDistToCell){
                                minDistToCell = distToCell;
                                closestCell = cell;
                                toClosestCell = toCell;
                            }
                        }
                    }
                }

                //second pass to find the distance to the closest edge
                float minEdgeDistance = 10;
                [unroll]
                for(int x2=-1; x2<=1; x2++){
                    [unroll]
                    for(int y2=-1; y2<=1; y2++){
                        [unroll]
                        for(int z2=-1; z2<=1; z2++){
                            float3 cell = baseCell + float3(x2, y2, z2);
                            float3 cellPosition = cell + rand3dTo3d(cell);
                            float3 toCell = cellPosition - value;

                            float3 diffToClosestCell = abs(closestCell - cell);
                            bool isClosestCell = diffToClosestCell.x + diffToClosestCell.y + diffToClosestCell.z < 0.1;
                            if(!isClosestCell){
                                float3 toCenter = (toClosestCell + toCell) * 0.5;
                                float3 cellDifference = normalize(toCell - toClosestCell);
                                float edgeDistance = dot(toCenter, cellDifference);
                                minEdgeDistance = min(minEdgeDistance, edgeDistance);
                            }
                        }
                    }
                }

                float random = rand3dTo1d(closestCell);
                return float3(minDistToCell, random, minEdgeDistance);
            }

            float3 rand1dTo3d(float value){
	            return float3(
		            rand1dTo1d(value, 3.9812),
		            rand1dTo1d(value, 7.1536),
		            rand1dTo1d(value, 5.7241)
	            );
            }

            Varyings vert(Attributes IN)
            {
                Varyings OUT;
                OUT.positionHCS = TransformObjectToHClip(IN.positionOS.xyz);
                OUT.uv = TRANSFORM_TEX(IN.uv, _BaseMap);
                OUT.positionOS = IN.positionOS.xyz;

                return OUT;
            }

            half4 frag(Varyings IN) : SV_Target
            {
                half4 color = SAMPLE_TEXTURE2D(_BaseMap, sampler_BaseMap, IN.uv) * _BaseColor;

                float3 value = IN.positionOS.xyz / _CellSize;
                float3 noise = voronoiNoise(value);

                float3 cellColor = rand1dTo3d(noise.y); 
                float valueChange = fwidth(value.z) * 0.5;
                float isBorder = 1 - smoothstep(0.05 - valueChange, 0.05 + valueChange, noise.z);
                float3 color1 = lerp(cellColor, float3(1.0, 1.0, 1.0), isBorder);

                return float4(color1, 1); 
            }
            ENDHLSL
        }
    }
}
